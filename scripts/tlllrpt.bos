
piece base,sleeves,turret,barrela,flare,body,barrelb,anneau;

#define SIG_AIM	2
#define SMOKEPIECE1 body

#include "EXPtype.h"
#include "smokeunit.h"

Create()
 { hide flare;
   dont-shade body;
   dont-cache flare;

   dont-cache sleeves;
   dont-cache turret;
   dont-cache barrela;
   dont-cache barrelb;
   dont-cache anneau;


   start-script SmokeUnit();
  }

AimPrimary(heading,pitch)
 { signal SIG_AIM;
   set-signal-mask SIG_AIM;
   turn turret to y-axis heading speed <10>;
   TURN sleeves to x-axis (0 - pitch) SPEED <15>;
   wait-for-turn turret around y-axis;
   wait-for-turn sleeves around x-axis;
   return(TRUE);
  }

FirePrimary()
 { 
	   show flare;
	   move barrela to z-axis [-1] speed [500];
	   move barrelb to z-axis [-4.5] speed [500];
	   sleep 150;
	   hide flare; 
	   move barrela to z-axis [0] speed [1];
	   move barrelb to z-axis [0] speed [1.5];
	   sleep 500;
  }

AimFromPrimary(piecenum)
 { piecenum=turret;
  }

QueryPrimary(piecenum)
 { piecenum=flare;
  }

SweetSpot(piecenum)
 { piecenum=turret;
  }

Killed( severity, corpsetype )
 { hide flare;
   explode barrela type FALL | SMOKE | EXPLODE_ON_HIT | BITMAP1;
   explode sleeves type FALL | SMOKE | EXPLODE_ON_HIT | BITMAP1;
   explode turret type SHATTER | EXPLODE_ON_HIT | BITMAP2;
   if (severity <= 25)
    { corpsetype = 1;
      return( 0 );
     }
   if (severity <= 50)
    { corpsetype = 2;
      return( 0 );
     }
   corpsetype = 3;
  }
